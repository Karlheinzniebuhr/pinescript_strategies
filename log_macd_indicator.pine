//@version=3
study(title="Ehlers-Smoothed Log-Space MACD [Krypt]", shorttitle="Log MACD [Krypt]", precision=3)

PI=3.14159265359

drop1st(src) =>
    x = na
    x := na(src[1]) ? na : src

EhlersSuperSmoother(src, lower) =>
	a1 = exp(-PI * sqrt(2) / lower)
	coeff2 = 2 * a1 * cos(sqrt(2) * PI / lower)
	coeff3 = -pow(a1, 2)
	coeff1 = (1 - coeff2 - coeff3) / 2
	filt = na
	filt := nz(coeff1 * (src + nz(src[1], src)) + coeff2 * filt[1] + coeff3 * nz(filt[2], filt[1]), src)

// Replaces built-in EMA function for better sensitivity in the beginning
xema(src, len) =>
    mult = 2.0 / (len + 1.0)
    res = na
    res := nz(mult * src + (1.0 - mult) * res[1], src)

// Reduce range of the output
LogClip(xs) =>
    xs > 0 ? log(1.0 + xs) : -log(1.0 - xs)

fastLength = input(12, minval=1, title="Fast Length")
slowLength = input(26, minval=1, title="Slow Length")
maLength=input(9, minval=1, title="MA Length")
signalSmoothing = input(3, minval=0, title="Signal Smoothing")
showsignals = input(false, title="Buy/Sell Signals")
logcompress = input(false, title="Extra Log Compression")
src = input(title="Source", type=source, defval=close)

price = log(drop1st(src))
fastMA = xema(price, fastLength)
slowMA = xema(price, slowLength)
rawsig = EhlersSuperSmoother(fastMA - slowMA, signalSmoothing)
sig = logcompress ? LogClip(rawsig) : rawsig
ma = sma(sig, maLength)
delta = sig - ma

plot(sig, color=#0094ff, style=line, title="MACD", transp=0)
plot(ma, color=#ff6a00, style=line, title="EMA", transp=0)
plot(delta, color=(delta > 0 ? #4985e7 : orange), style=histogram, title="Histogram")

// Buy/Sell Signals

// use curvature information to filter out some false positives
midpoint = 0.0
mm1 = change(change(ma, 1), 1)
mm2 = change(change(ma, 2), 2)
sellsignals = showsignals and (mm1 < 0 and mm2 < 0) and crossunder(sig, ma) and sig[1] > midpoint
buysignals = showsignals and (mm1 > 0 and mm2 > 0) and crossover(sig, ma) and sig[1] < midpoint

off = 1.1
plot(buysignals ? off * sig[1] : na, style=circles, color=#008fff, linewidth=3, title="Buy Signal", transp=0)
plot(sellsignals ? off * sig[1] : na, style=circles, color=#ff0000, linewidth=3, title="Sell Signal", transp=0)
