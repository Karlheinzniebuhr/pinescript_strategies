//@version=5
indicator(title="Backtest optimized Smoothed Log-Space MACD [elindicador.ai]", shorttitle="Backtest optimized Log MACD [elindicador.ai]", precision=3)

PI=3.14159265359

drop1st(src) =>
    x = float(na)
    x := na(src[1]) ? na : src

EhlersSuperSmoother(src, lower) =>
	a1 = math.exp(-PI * math.sqrt(2) / lower)
	coeff2 = 2 * a1 * math.cos(math.sqrt(2) * PI / lower)
	coeff3 = -math.pow(a1, 2)
	coeff1 = (1 - coeff2 - coeff3) / 2
	filt = float(na)
	filt := nz(coeff1 * (src + nz(src[1], src)) + coeff2 * filt[1] + coeff3 * nz(filt[2], filt[1]), src)

// Replaces built-in EMA function for better sensitivity in the beginning
xema(src, len) =>
    mult = 2.0 / (len + 1.0)
    res = float(na)
    res := nz(mult * src + (1.0 - mult) * res[1], src)

// Reduce range of the output
LogClip(xs) =>
    xs > 0 ? math.log(1.0 + xs) : -math.log(1.0 - xs)

fastLength = input.int(3, minval=1, title="Fast Length")
slowLength = input.int(57, minval=1, title="Slow Length")
maLength = input.int(155, minval=1, title="MA Length")
signalSmoothing = input.int(5, minval=0, title="Signal Smoothing")
logcompress = input(false, title="Extra Log Compression")
src = input.source(title="Source", defval=close)
long_threshold = input.float(-0.03, title="Long Threshold", step=0.01)
short_threshold = input.float(0.1, title="Short Threshold", step=0.01)

price = math.log(drop1st(src))
fastMA = xema(price, fastLength)
slowMA = xema(price, slowLength)
rawsig = EhlersSuperSmoother(fastMA - slowMA, signalSmoothing)
sig = logcompress ? LogClip(rawsig) : rawsig
ma = ta.sma(sig, maLength)
delta = sig - ma

plot(sig, color=#FF6D00, title="MACD")
plot(ma, color=#2962FF, title="EMA")
plot(delta, style=plot.style_columns, color=(delta > 0 ? #4985e7 : color.orange), title="Histogram", transp=50)

// Buy/Sell Signals
// use curvature information to filter out some false positives
mm1 = ta.change(ta.change(ma, 1), 1)
mm2 = ta.change(ta.change(ma, 2), 2)

buysignal = (mm1 > 0 and mm2 > 0) and ta.crossover(sig, ma) and sig[1] > long_threshold
sellsignal = (mm1 < 0 and mm2 < 0) and ta.crossunder(sig, ma) and sig[1] < short_threshold
drawbuysignal = ta.barssince(sellsignal) <= ta.barssince(buysignal[1]) and buysignal
drawsellsignal = ta.barssince(buysignal) <= ta.barssince(sellsignal[1]) and sellsignal

off = 1.1
plot(drawbuysignal ? off * sig[1] : na,  style=plot.style_circles, color=#008fff, linewidth=3, title="Buy Signal", transp=0)
plot(drawsellsignal ? off * sig[1] : na,  style=plot.style_circles, color=#ff0000, linewidth=3, title="Sell Signal", transp=0)
